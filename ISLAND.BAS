   10 REM UNLOCK THE TILE ENGINE FEATURE FLAG
   20 VDU 23,0,248,768;0;
   30 :
   40 INPUT "USE DOUBLE BUFFERED MODE? (Y/N) " A$
   50 H$="N"
   60 V$="Y"
   70 :
   80 REM INIT TILEBANK
   90 VDU 23,0,194,0,0,0,0,0
  100 :
  110 REM LOAD TILES
  120 CLS
  130 PRINT
  140 PRINT
  150 DIM TILEDATA%(63)
  160 FOR TILEID%=1 TO 24
  170   FILENAME$="NT"+STR$(TILEID%)+".TILE"
  180   PRINT "Loading "+FILENAME$
  190   PROCloadtiledata(FILENAME$)
  200   PROCcreatetile(TILEID%)
  210   PRINT "Tile created: "+STR$(TILEID%)
  220   VDU 23,0,194,6,0,TILEID%,0,TILEID%,0,0,0,0
  230 NEXT TILEID%
  240 :
  250 REM INIT TILE MAP
  260 VDU 23,0,194,16,0,3,0,0
  270 :
  280 REM SET TILEMAP
  290 TILEMAPWIDTH%=63
  300 TILEMAPHEIGHT%=31
  310 DIM TILEMAP%(TILEMAPWIDTH%,TILEMAPHEIGHT%)
  320 DIM TILEATTRIB%(TILEMAPWIDTH%,TILEMAPHEIGHT%)
  330 PROCloadtilemap
  340 FOR Y%=0 TO 31
  350   FOR X%=0 TO 63
  360     REM PRINT "Setting Tile Map: X:"+STR$(X%)+" Y:"+STR$(Y%)+" TILE:"+STR$(TILEMAP%(X%,Y%))+" Attribute:"+STR$(TILEATTRIB%(X%,Y%))
  370     VDU 23,0,194,17,0,X%,Y%,TILEMAP%(X%,Y%),TILEATTRIB%(X%,Y%)
  380   NEXT X%
  390 NEXT Y%
  400 :
  410 REM INIT TILE LAYER
  420 VDU 23,0,194,24,0,2,0,0
  430 :
  440 REM DRAW THE SCREEN
  450 IF A$="Y" THEN MODE 136 ELSE MODE 8
  460 COLOUR 140
  470 C%=0
  480 XPOS%=0
  490 YPOS%=0
  500 XOFFSET%=0
  510 YOFFSET%=0
  520 REPEAT
  530   IF H$="Y" THEN PROCscrollhorizontal
  540   IF V$="Y" THEN PROCscrollvertical
  550   :
  560   REM SET SCROLL PARAMETERS
  570   VDU 23,0,194,26,0,XPOS%,YPOS%,XOFFSET%,YOFFSET%
  580   :
  590   REM AS ALL TILES ARE FULLY OPAQUE, NO NEED TO CLS TO REDRAW BACKGROUND COLOUR
  600   :
  610   REM UPDATE AND DRAW BUFFER
  620   VDU 23,0,194,30,0
  630   :
  640   REM DRAW BORDER
  650   REM IF A$="Y" THEN VDU 23,0,196,1
  660   :
  670   REM SWITCH BUFFERS / WAIT FOR VSYNC
  680   VDU 23,0,195
  690   :
  700   C%=C%+1
  710 UNTIL C%=600
  720 MODE 3
  730 :
  740 :
  750 END
  760 :
  770 DEFPROCscrollhorizontal
  780 XOFFSET%=XOFFSET%+1
  790 IF XOFFSET%>7 THEN XOFFSET%=0:XPOS%=XPOS%+1
  800 IF XPOS%>TILEMAPWIDTH% THEN XPOS%=0
  810 ENDPROC
  820 :
  830 DEFPROCscrollvertical
  840 YOFFSET%=YOFFSET%-1 
  850 IF YOFFSET%<0 THEN YOFFSET%=7:YPOS%=YPOS%-1
  860 IF YPOS%<0 THEN YPOS%=TILEMAPHEIGHT%
  870 ENDPROC
  880 : 
  890 DEFPROCloadtiledata(FILENAME$)
  900 LOCAL X%,FILE%
  910 FILE%=OPENIN(FILENAME$)
  920 VERSION%=BGET#FILE%
  930 BPP%=BGET#FILE%
  940 SIZE%=BGET#FILE%
  950 NUM%=BGET#FILE%
  960 FOR X%=0 TO 63
  970   TILEDATA%(X%)=BGET#FILE%
  980 NEXT X%
  990 CLOSE#FILE%
 1000 ENDPROC
 1010 :
 1020 DEFPROCcreatetile(TILEID%)
 1030 LOCAL X%, P%
 1040 VDU 23,0,194,1,0,TILEID%
 1050 FOR X%=0 TO 63
 1060   P%=TILEDATA%(X%)
 1070   VDU P%
 1080 NEXT X%
 1090 ENDPROC  
 1100 :
 1110 DEFPROCloadtilemap
 1120 LOCAL FILENAME$, X%, Y%, T%, A%
 1130 FILENAME$="ISLAND.MAP"
 1140 FILE%=OPENIN(FILENAME$)
 1150 MAPVERSION%=BGET#FILE%
 1160 MAPWIDTH%=BGET#FILE%
 1170 MAPHEIGHT%=BGET#FILE%
 1180 MAPEXTRA%=BGET#FILE%
 1190 FOR Y%=0 TO 31
 1200   FOR X%=0 TO 63
 1210     T%=BGET#FILE%
 1220     TILEMAP%(X%,Y%)=T%
 1230     A%=BGET#FILE%
 1240     TILEATTRIB%(X%,Y%)=A%
 1250   NEXT X%
 1260 NEXT Y%
 1270 CLOSE#FILE%
 1280 ENDPROC
