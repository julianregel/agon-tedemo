   10 REM UNLOCK THE TILE ENGINE FEATURE FLAG
   20 VDU 23,0,248,768;0;
   30 :
   40 INPUT "USE DOUBLE BUFFERED MODE? (Y/N) " A$
   50 H$="Y"
   60 V$="N"
   70 :
   80 REM INIT TILEBANK
   90 VDU 23,0,194,0,0,0,0,0
  100 :
  110 REM LOAD TILES
  120 CLS
  130 PRINT
  140 PRINT
  150 DIM TILEDATA%(63)
  160 FOR TILEID%=1 TO 10
  170   FILENAME$="NT"+STR$(TILEID%)+".TILE"
  180   PRINT "Loading "+FILENAME$
  190   PROCloadtiledata(FILENAME$)
  200   PROCcreatetile(TILEID%)
  210   PRINT "Tile created: "+STR$(TILEID%)
  220   VDU 23,0,194,6,0,TILEID%,0,TILEID%,0,0,0,0
  230 NEXT TILEID%
  240 :
  250 REM INIT TILE MAP
  260 VDU 23,0,194,16,0,3,0,0
  270 :
  280 REM SET TILEMAP
  290 TILEMAPWIDTH%=63
  300 TILEMAPHEIGHT%=31
  310 DIM TILEMAP%(TILEMAPWIDTH%,TILEMAPHEIGHT%)
  320 DIM TILEATTRIB%(TILEMAPWIDTH%,TILEMAPHEIGHT%)
  330 PROCloadtilemap
  340 FOR Y%=0 TO 31
  350   FOR X%=0 TO 63
  360     REM PRINT "Setting Tile Map: X:"+STR$(X%)+" Y:"+STR$(Y%)+" TILE:"+STR$(TILEMAP%(X%,Y%))+" Attribute:"+STR$(TILEATTRIB%(X%,Y%))
  370     VDU 23,0,194,17,0,X%,Y%,TILEMAP%(X%,Y%),TILEATTRIB%(X%,Y%)
  380   NEXT X%
  390 NEXT Y%
  400 :
  410 REM INIT TILE LAYER
  420 VDU 23,0,194,24,0,2,0,0
  430 :
  440 REM DRAW THE SCREEN
  450 IF A$="Y" THEN MODE 136 ELSE MODE 8
  460 COLOUR 140
  470 C%=0
  480 XPOS%=0
  490 YPOS%=0
  500 XOFFSET%=0
  510 YOFFSET%=0
  520 REPEAT
  530   IF H$="Y" THEN PROCscrollhorizontal
  540   IF V$="Y" THEN PROCscrollvertical
  550   :
  560   REM SET SCROLL PARAMETERS
  570   VDU 23,0,194,26,0,XPOS%,YPOS%,XOFFSET%,YOFFSET%
  580   :
  590   REM NEED TO CLS TO DRAW BACKGROUND COLOUR
  600   CLS
  610   :
  620   REM UPDATE AND DRAW BUFFER
  630   VDU 23,0,194,30,0
  640   :
  650   REM DRAW BORDER
  660   REM IF A$="Y" THEN VDU 23,0,196,1
  670   :
  680   REM SWITCH BUFFERS / WAIT FOR VSYNC
  690   VDU 23,0,195
  700   :
  710   C%=C%+1
  720 UNTIL C%=1200
  730 MODE 3
  740 :
  750 :
  760 END
  770 :
  780 DEFPROCscrollhorizontal
  790 XOFFSET%=XOFFSET%+1
  800 IF XOFFSET%>7 THEN XOFFSET%=0:XPOS%=XPOS%+1
  810 IF XPOS%>TILEMAPWIDTH% THEN XPOS%=0
  820 ENDPROC
  830 :
  840 DEFPROCscrollvertical
  850 YOFFSET%=YOFFSET%-1 
  860 IF YOFFSET%<0 THEN YOFFSET%=7:YPOS%=YPOS%-1
  870 IF YPOS%<0 THEN YPOS%=TILEMAPHEIGHT%
  880 ENDPROC
  890 : 
  900 DEFPROCloadtiledata(FILENAME$)
  910 LOCAL X%,FILE%
  920 FILE%=OPENIN(FILENAME$)
  930 VERSION%=BGET#FILE%
  940 BPP%=BGET#FILE%
  950 SIZE%=BGET#FILE%
  960 NUM%=BGET#FILE%
  970 FOR X%=0 TO 63
  980   TILEDATA%(X%)=BGET#FILE%
  990 NEXT X%
 1000 CLOSE#FILE%
 1010 ENDPROC
 1020 :
 1030 DEFPROCcreatetile(TILEID%)
 1040 LOCAL X%, P%
 1050 VDU 23,0,194,1,0,TILEID%
 1060 FOR X%=0 TO 63
 1070   P%=TILEDATA%(X%)
 1080   VDU P%
 1090 NEXT X%
 1100 ENDPROC  
 1110 :
 1120 DEFPROCloadtilemap
 1130 LOCAL FILENAME$, X%, Y%, T%, A%
 1140 FILENAME$="MARIO.MAP"
 1150 FILE%=OPENIN(FILENAME$)
 1160 MAPVERSION%=BGET#FILE%
 1170 MAPWIDTH%=BGET#FILE%
 1180 MAPHEIGHT%=BGET#FILE%
 1190 MAPEXTRA%=BGET#FILE%
 1200 FOR Y%=0 TO 31
 1210   FOR X%=0 TO 63
 1220     T%=BGET#FILE%
 1230     TILEMAP%(X%,Y%)=T%
 1240     A%=BGET#FILE%
 1250     TILEATTRIB%(X%,Y%)=A%
 1260   NEXT X%
 1270 NEXT Y%
 1280 CLOSE#FILE%
 1290 ENDPROC
