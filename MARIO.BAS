   10 INPUT "USE DOUBLE BUFFERED MODE? (Y/N) " A$
   20 H$="Y"
   30 V$="N"
   40 :
   50 REM INIT TILEBANK
   60 VDU 23,0,194,0,0,0,0,0
   70 :
   80 REM LOAD TILES
   90 CLS
  100 PRINT
  110 PRINT
  120 DIM TILEDATA%(63)
  130 FOR TILEID%=1 TO 10
  140   FILENAME$="NT"+STR$(TILEID%)+".TILE"
  150   PRINT "Loading "+FILENAME$
  160   PROCloadtiledata(FILENAME$)
  170   PROCcreatetile(TILEID%)
  180   PRINT "Tile created: "+STR$(TILEID%)
  190   VDU 23,0,194,6,0,TILEID%,0,TILEID%,0,0,0,0
  200 NEXT TILEID%
  210 :
  250 REM INIT TILE MAP
  260 VDU 23,0,194,16,0,3,0,0
  270 :
  280 REM SET TILEMAP
  282 TILEMAPWIDTH%=63
  284 TILEMAPHEIGHT%=31
  290 DIM TILEMAP%(TILEMAPWIDTH%,TILEMAPHEIGHT%)
  300 DIM TILEATTRIB%(TILEMAPWIDTH%,TILEMAPHEIGHT%)
  310 PROCloadtilemap
  320 FOR Y%=0 TO 31
  330   FOR X%=0 TO 63
  335     REM PRINT "Setting Tile Map: X:"+STR$(X%)+" Y:"+STR$(Y%)+" TILE:"+STR$(TILEMAP%(X%,Y%))+" Attribute:"+STR$(TILEATTRIB%(X%,Y%))
  340     VDU 23,0,194,17,0,X%,Y%,TILEMAP%(X%,Y%),TILEATTRIB%(X%,Y%)
  350   NEXT X%
  360 NEXT Y%
  370 :
  380 REM INIT TILE LAYER
  390 VDU 23,0,194,24,0,2,0,0
  400 :
  410 REM DRAW THE SCREEN
  420 IF A$="Y" THEN MODE 136 ELSE MODE 8
  430 COLOUR 140
  440 C%=0
  450 XPOS%=0
  460 YPOS%=0
  470 XOFFSET%=0
  480 YOFFSET%=0
  490 REPEAT
  500   IF H$="Y" THEN PROCscrollhorizontal
  510   IF V$="Y" THEN PROCscrollvertical
  520   :
  530   REM SET SCROLL PARAMETERS
  540   VDU 23,0,194,26,0,XPOS%,YPOS%,XOFFSET%,YOFFSET%
  550   :
  560   REM NEED TO CLS TO DRAW BACKGROUND COLOUR
  580   CLS
  590   :
  600   REM DRAW ROW BUFFER
  610   VDU 23,0,194,30,0
  620   :
  630   REM DRAW BORDER
  640   REM IF A$="Y" THEN VDU 23,0,196,1
  650   :
  660   REM SWITCH BUFFERS / WAIT FOR VSYNC
  670   VDU 23,0,195
  680   :
  690   C%=C%+1
  700 UNTIL C%=1200
  710 MODE 3
  720 :
  730 :
  740 END
  750 :
  760 DEFPROCscrollhorizontal
  770 XOFFSET%=XOFFSET%+1
  780 IF XOFFSET%>7 THEN XOFFSET%=0:XPOS%=XPOS%+1
  790 IF XPOS%=TILEMAPWIDTH%+1 THEN XPOS%=0
  800 ENDPROC
  810 :
  820 DEFPROCscrollvertical
  830 YOFFSET%=YOFFSET%-1 
  840 IF YOFFSET%<0 THEN YOFFSET%=7:YPOS%=YPOS%-1
  850 IF YPOS%<0 THEN YPOS%=TILEMAPHEIGHT%
  860 ENDPROC
  870 : 
  880 DEFPROCloadtiledata(FILENAME$)
  890 LOCAL X%,FILE%
  900 FILE%=OPENIN(FILENAME$)
  910 VERSION%=BGET#FILE%
  920 BPP%=BGET#FILE%
  930 SIZE%=BGET#FILE%
  940 NUM%=BGET#FILE%
  950 FOR X%=0 TO 63
  960   TILEDATA%(X%)=BGET#FILE%
  970 NEXT X%
  980 CLOSE#FILE%
  990 ENDPROC
 1000 :
 1010 DEFPROCcreatetile(TILEID%)
 1020 LOCAL X%, P%
 1030 VDU 23,0,194,1,0,TILEID%
 1040 FOR X%=0 TO 63
 1050   P%=TILEDATA%(X%)
 1060   VDU P%
 1070 NEXT X%
 1080 ENDPROC  
 1090 :
 1100 DEFPROCloadtilemap
 1110 LOCAL FILENAME$, X%, Y%, T%, A%
 1120 FILENAME$="MARIO.MAP"
 1130 FILE%=OPENIN(FILENAME$)
 1140 MAPVERSION%=BGET#FILE%
 1150 MAPWIDTH%=BGET#FILE%
 1160 MAPHEIHT%=BGET#FILE%
 1170 MAPEXTRA%=BGET#FILE%
 1180 FOR Y%=0 TO 31
 1190   FOR X%=0 TO 63
 1200     T%=BGET#FILE%
 1210     TILEMAP%(X%,Y%)=T%
 1220     A%=BGET#FILE%
 1230     TILEATTRIB%(X%,Y%)=A%
 1240   NEXT X%
 1250 NEXT Y%
 1260 CLOSE#FILE%
 1270 ENDPROC
